/**
 *  Root-level SimEthereal build script.
 */

plugins {
    id 'sim-ethereal.published-library'
}

version='1.7.1-SNAPSHOT'
group='com.simsilica'

ext.jmeVersion = "3.1.0-stable"
ext.slf4jVersion = '1.7.32'
ext.simMathVersion = '1.4.0'

// Set this module's maven pom description
publishing.publications.library(MavenPublication).pom {
    description = 'A real-time network sync library in Java.'
}

dependencies {
    // Based on JME networking
    api "org.jmonkeyengine:jme3-networking:$jmeVersion"

    // Uses SimMath for bit streaming utilities and transition buffers
    api "com.simsilica:sim-math:$simMathVersion"

    // Base logging
    implementation "org.slf4j:slf4j-api:$slf4jVersion"
}

compileJava.doLast {
    def buildDate = new Date().format('yyyyMMdd')
    println "Writing sim-ethereal.build.date:" + buildDate
    new File(destinationDir, 'sim-ethereal.build.date').text = buildDate
}

